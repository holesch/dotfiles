set nocompatible " use Vim settings

" === Vim configuration ===
set tabstop=4 " Set tab width to 4 spaces
set shiftwidth=4 " When indenting with '>', use 4 spaces width
set softtabstop=4 " Delete 4 spaces with backspace
set expandtab " On pressing tab, insert 4 spaces
set ignorecase " Ignore case when searching
set smartcase " Don't ignore case if there is an uppercase letter
set relativenumber " Display relative line numbers
set number " Display absolute line number on current line
set showcmd " Display command while typing
set lazyredraw " The screen will not be redrawn while executing macros
set visualbell " Use visual bell instead of beeping
set colorcolumn=50,72,80 " Highlight screen columns
set wildmode=longest:full,full " Complete commands till longest common string
set viminfo='1000,<500,s100,h,% " Set what to restore upon startup
set hlsearch " Highlight search matches
set clipboard=unnamed " Use system clipboard
set path+=** " Always search recursively
set listchars=tab:▸\ ,eol:¬ " Set whitespace characters
set list " Show whitespace
set linebreak " Wrap only at whitespace characters
set spelllang=en_us " Set spell check region to American English
set noshowmode " Hide the current mode in the last line
set completeopt-=preview " Hide the preview window
set autowrite " Save the file when jumping away

if has("cscope")
    set cscopequickfix=s-,c-,d-,i-,t-,e-,a- " Use quickfix window with cscope
    set cscopetag " Use cscope with :tag and CTRL-]
    " add any database in current directory
    if filereadable("cscope.out")
        cs add cscope.out
        " else add database pointed to by environment
    elseif $CSCOPE_DB != ""
        cs add $CSCOPE_DB
    endif
    set cscopeverbose " Print message when adding a database

    nnoremap <C-_>s :cs find s <C-R>=expand("<cword>")<CR><CR>
    nnoremap <C-_>g :cs find g <C-R>=expand("<cword>")<CR><CR>
    nnoremap <C-_>c :cs find c <C-R>=expand("<cword>")<CR><CR>
    nnoremap <C-_>t :cs find t <C-R>=expand("<cword>")<CR><CR>
    nnoremap <C-_>e :cs find e <C-R>=expand("<cword>")<CR><CR>
    nnoremap <C-_>f :cs find f <C-R>=expand("<cfile>")<CR><CR>
    nnoremap <C-_>i :cs find i ^<C-R>=expand("<cfile>")<CR>$<CR>
    nnoremap <C-_>d :cs find d <C-R>=expand("<cword>")<CR><CR>
    nnoremap <C-_>a :cs find a <C-R>=expand("<cword>")<CR><CR>
endif

" === GUI configuration ===
set guifont=Menlo\ Regular:h12 " Set font
set guioptions= " Hide scroll bar, menu, tabs, etc.

" === Functions ===
function! Preserve(command)
    " Preparation: save last search, and cursor position.
    let _s=@/
    let l = line(".")
    let c = col(".")
    " Execute the command
    execute a:command
    " Clean up: restore previous search history, and cursor position
    let @/=_s
    call cursor(l, c)
endfunction

" === Key mappings ===
" Strip trailing whitespace
nnoremap <leader>$ :call Preserve("%s/\\s\\+$//e")<CR>

" Reindent file
nnoremap <leader>= :call Preserve("normal gg=G")<CR>

" Move between wrapped lines
noremap <C-j> gj
noremap <C-k> gk

" Expand %% to directory of current file
cnoremap %% <C-R>=fnameescape(expand('%:h')).'/'<cr>

" Open vimrc in a new tab
nnoremap <leader>v :tabedit $MYVIMRC<CR>

" Select the text that was last edited/pasted
nnoremap gV `[v`]

" Toggle the undo graph
nnoremap <F5> :GundoToggle<CR>

" Switch between windows
nnoremap <leader>h <C-w><C-h>
nnoremap <leader>j <C-w><C-j>
nnoremap <leader>k <C-w><C-k>
nnoremap <leader>l <C-w><C-l>

" Save buffer
nnoremap ZS :write<CR>

" === Auto commands ===
if has("autocmd")
    augroup user
        autocmd!
        " Source the vimrc file after saving it
        autocmd BufWritePost vimrc source $MYVIMRC | AirlineRefresh
    augroup END
endif

" === Colorscheme ===
colorscheme hybrid " Use the 'hybrid' colorscheme
set background=dark " Use dark background

augroup vimrc_color
    autocmd!
    autocmd ColorScheme hybrid highlight! link QuickFixLine CursorLine
    autocmd ColorScheme hybrid highlight! link WildMenu Search
augroup END

" === Plugin configuration ===
" --- Airline ---
let g:airline_theme = 'hybrid'
let g:airline#extensions#tabline#enabled = 1
let g:airline#extensions#tabline#tab_nr_type = 1 " Show tab number
let g:airline#extensions#tabline#show_tab_type = 0 " Hide tab type
let g:airline#extensions#tabline#show_close_button = 0 " Hide close button

" --- Neocomplete ---
if has('lua')
    let g:neocomplete#enable_at_startup = 1 " Use neocomplete.
    let g:neocomplete#enable_smart_case = 1 " Use smartcase.
    inoremap <expr> <C-g> neocomplete#undo_completion()
    inoremap <expr> <C-l> neocomplete#complete_common_string()

    " close popup and add newline
    inoremap <silent> <CR> <C-r>=<SID>my_cr_function()<CR>

    " <TAB> completion
    inoremap <silent><expr> <TAB>
          \ pumvisible() ? "\<C-n>" :
          \ <SID>is_white_space_before_cursor() ? "\<TAB>" :
          \ neocomplete#start_manual_complete()

    " close popup and delete char
    inoremap <expr><C-h> neocomplete#smart_close_popup()."\<C-h>"
    inoremap <expr><BS> neocomplete#smart_close_popup()."\<C-h>"

    function! s:my_cr_function()
      return (pumvisible() ? "\<C-y>" : "" ) . "\<CR>"
    endfunction

    function! s:is_white_space_before_cursor() abort
      let col = col('.') - 1
      return !col || getline('.')[col - 1]  =~ '\s'
    endfunction

    " Enable auto omni completion.
    if !exists('g:neocomplete#sources#omni#input_patterns')
      let g:neocomplete#sources#omni#input_patterns = {}
    endif
    let g:neocomplete#sources#omni#input_patterns.c =
    \ '[^.[:digit:] *\t]\%(\.\|->\)\%(\h\w*\)\?'
    let g:neocomplete#sources#omni#input_patterns.cpp =
    \ '[^.[:digit:] *\t]\%(\.\|->\)\%(\h\w*\)\?\|\h\w*::\%(\h\w*\)\?'
endif

" --- Syntastic ---
let g:syntastic_always_populate_loc_list = 1
let g:syntastic_auto_loc_list = 1
let g:syntastic_check_on_open = 1
let g:syntastic_check_on_wq = 0
let g:syntastic_error_symbol = "✗"
let g:syntastic_warning_symbol = "⚠"
let g:syntastic_style_error_symbol = "S✗"
let g:syntastic_style_warning_symbol = "S⚠"
let g:syntastic_enable_balloons = 0
let g:syntastic_loc_list_height = 5

